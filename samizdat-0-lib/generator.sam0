# Copyright 2013 the Samizdat Authors (Dan Bornstein et alia).
# Licensed AS IS and WITHOUT WARRANTY under the Apache License,
# Version 2.0. Details: <http://www.apache.org/licenses/LICENSE-2.0>

#
# Generator functions
#


#
# Helper definitions
#

# Standard "voided" generator. This is a generator, not a generator
# constructor.
fn voidedGenerator(box) {
    boxSet(box)
};

# Empty list generator. It always yields the empty list and returns
# itself. This is a generator, not a generator constructor.
fn emptyListGenerator(box) {
    boxSet(box, []);
    <> emptyListGenerator
};

# General sequential-incrementing-value generator. Takes a "filter" function
# (function from int to the proper actual value, returns void for
# out-of-range int), an int starting value, and an int increment.
fn makeIncrementGenerator(filterFunction, first, increment) {
    # Main generator maker.
    fn makeIncrementGenerator1(value) {
        <> { box ::
            <> ifValue { <> filterFunction(value) }
                { result ::
                    boxSet(box, result);
                    <> makeIncrementGenerator1(iadd(value, increment));
                }
                {
                    # The filter indicated that the generator is voided.
                    boxSet(box);
                }
        }
    };

    <> makeIncrementGenerator1(first)
};

# Makes a generator for an int.
fn makeIntGenerator(int) {
    def size = lowSize(int);

    <> makeIncrementGenerator(0, 1)
        { index :: <> ibit(int, &gt(size, index)) }
};

# Makes a generator for a list.
fn makeListGenerator(list) {
    <> makeIncrementGenerator(0, 1)
        { index :: <> listNth(list, index) }
};

# Makes a generator for a map.
fn makeMapGenerator(map) {
    <> makeIncrementGenerator(0, 1)
        { index :: <> mapNth(map, index) }
};

# Makes a generator for a string.
fn makeStringGenerator(string) {
    <> makeIncrementGenerator(0, 1)
        { index :: <> stringNth(string, index) }
};

# Degenerate generator maker for uniqlets, which are tacitly assumed to
# already be generators.
fn makeUniqletGenerator(uniqlet) {
    <> uniqlet
};

# Map from low-layer value types to corresponding appropriate generator
# maker functions.
def GENERATOR_MAKERS = [
    int:     makeIntGenerator,
    list:    makeListGenerator,
    map:     makeMapGenerator,
    string:  makeStringGenerator,
    uniqlet: makeUniqletGenerator
];

# Map from `[direction, inclusive]` pairs to appropriate test functions.
# Note that in order to have the right pass-through test result, the test
# is called as `test(limit, value)` and not with the arguments the other
# way around.
def INT_RANGE_TESTS = [
    [-1, false]: lt,
    [-1, true]:  le,
    [1,  false]: gt,
    [1,  true]:  ge
];

# Generic generator maker for int and single-character string ranges. If
# `optLimit` is passed, then `optInclusive` needs to be as well.
fn makeRangeGenerator(first, increment, optLimit?, optInclusive?) {
    # This test also ends up guaranteeing that `first` is either an int
    # or a single-character string, which is why it goes before the
    # zero-increment special case.
    def firstInt = ifIs { <> isInt(first) }
        { <> first }
        { <> intFromChar(first) };

    ifIs { <> eq(increment, 0) }
        {
            # Special case: A range with increment of zero always just
            # yields the `first` value and then becomes voided.
            return makeListGenerator([first])
        };

    def limitFunction = ifValue { <> optLimit* }
        { limit ::
            def limitInt = ifIs { <> isInt(limit) }
                {
                    <> ifIs { <> isInt(first) }
                        { <> limit }
                        { io0Die("Type mismatch between first and limit.") };
                }
                { <> intFromChar(limit) };
            def testFunction =
                mapGet(INT_RANGE_TESTS, [isign(increment), (optInclusive*)]);
            <> { x :: <> testFunction(limitInt, x) }
        }
        {
            # Open range; no limit test.
            <> { x :: <> x }
        };

    def filterFunction = ifIs { <> isInt(first) }
        { <> limitFunction }
        { <> { x :: <> charFromInt(&limitFunction(x)) } };

    <> makeIncrementGenerator(filterFunction, firstInt, increment)
};

# Helper for `paraGenerator`, which implements the degenerate case of
# a single inner generator. This can end up being usefully used in a few
# cases, e.g. by virtue of `filterGenerator` always calling `paraGenerator`.
fn oneItemParaGenerator(generator) {
    <> { <out> box ::
        def innerBox = yieldBox();
        <> ifValue { <> generator(innerBox) }
            { nextGenerator ::
                boxSet(box, [boxGet(innerBox)]);
                <> oneItemParaGenerator(nextGenerator)
            }
    }
};


#
# Exported functions
#

# Documented in Samizdat Layer 0 spec.
fn collectFilter(filterFunction, generators*) {
    ifIs { <> eq(lowSize(generators), 1) }
        {
            def generator = generators*;
            ifIs { <> isList(generator) } {
                # We are filtering a single list, so just use the
                # special-case (and more efficient) `listFilter`.
                return listFilter(filterFunction, generator)
            }
        };

    <> [filterGenerator(filterFunction, generators*)*]
};

# Documented in Samizdat Layer 0 spec.
fn collectGenerator(generator) {
    # Avoid pointless list->generator->list conversion.
    ifIs { <> isList(generator) }
        { return generator };

    <> [generatorFromValue(generator)*]
};

# Documented in Samizdat Layer 0 spec.
fn doFilter(filterFunction, generators*) {
    doGenerator(filterGenerator(filterFunction, generators*))
};

# Documented in Samizdat Layer 0 spec.
fn doGenerator(generator) {
    loopReduce(generatorFromValue(generator)) { gen ::
        <> ifValue { <> gen(nullBox) }
            { nextGen :: <> [nextGen] }
            { return }
    }
};

# Documented in Samizdat Layer 0 spec.
fn doReduce(reduceFunction, generators, baseValues) {
    def gen = paraGenerator(generators*);

    loopReduce(gen, baseValues)
        { gen, values ::
            def box = yieldBox();
            <> ifValue { <> gen(box) }
                { nextGenerator ::
                    <> ifValue { <> reduceFunction(boxGet(box)*, values*) }
                        { nextValues :: <> [nextGenerator, nextValues] }
                        {
                            # The reducer discarded the values.
                            <> [nextGenerator, values]
                        }
                }
                {
                    # The reduce generator was voided.
                    return values
                }
        }
};

# Documented in Samizdat Layer 0 spec.
fn doReduce1(reduceFunction, generator, baseValue) {
    loopReduce(generatorFromValue(generator), baseValue)
        { gen, value ::
            def box = yieldBox();
            <> ifValue { <> gen(box) }
                { nextGenerator ::
                    <> ifValue { <> reduceFunction(boxGet(box), value) }
                        { nextValue :: <> [nextGenerator, nextValue] }
                        {
                            # The reducer discarded the values.
                            <> [nextGenerator, value]
                        }
                }
                {
                    # The reduce generator was voided.
                    return value
                }
        }
};

# Documented in Samizdat Layer 0 spec.
fn exclusiveRange(first, increment, limit) {
    <> makeRangeGenerator(first, increment, limit, false)
};

# Documented in Samizdat Layer 0 spec.
fn filterGenerator(filterFunction, generators*) {
    # Main generator maker, which takes a generator (per se) as its argument.
    fn filterGenerator1(gen) {
        <> { <out> box ::
            # This loop handles the fact that the filter function is
            # allowed to yield void to indicate a generated value is to
            # be discarded.
            loopReduce(gen) { <next> gen ::
                def innerBox = yieldBox();
                ifValue { <> gen(innerBox) }
                    { nextGenerator ::
                        ifValue { <> filterFunction(boxGet(innerBox)*) }
                            { value ::
                                boxSet(box, value);
                                <out> filterGenerator1(nextGenerator)
                            }
                            {
                                # The filter discarded the value. Iterate!
                                <next> [nextGenerator]
                            }
                    };

                # The inner generator was voided.
                boxSet(box);
                <out>
            }
        }
    };

    <> filterGenerator1(paraGenerator(generators*))
};

# Documented in Samizdat Layer 0 spec.
fn generatorFromValue(value) {
    <> ifValue { <> mapGet(GENERATOR_MAKERS, lowType(value)) }
        { maker :: <> maker(value) }
        { io0Die("Invalid generator source.") }
};

# Documented in Samizdat Layer 0 spec.
fn inclusiveRange(first, increment, limit) {
    <> makeRangeGenerator(first, increment, limit, true)
};

# Documented in Samizdat Layer 0 spec.
fn mapFromGenerator(generator) {
    # Avoid pointless map->generator->map conversion.
    ifIs { <> isMap(generator) }
        { return generator };

    <> mapAdd(generatorFromValue(generator)*)
};

# Documented in Samizdat Layer 0 spec.
fn openRange(first, increment) {
    <> makeRangeGenerator(first, increment)
};

# Documented in Samizdat Layer 0 spec.
fn optGenerator(generator) {
    # Main generator maker, which takes a generator (per se) as its argument.
    fn optGenerator1(gen) {
        <> { box ::
            def innerBox = yieldBox();
            <> ifValue { <> gen(innerBox) }
                { nextGenerator ::
                    boxSet(box, [boxGet(innerBox)]);
                    <> optGenerator1(nextGenerator)
                }
                { <> emptyListGenerator(box) }
        }
    };

    <> optGenerator1(generatorFromValue(generator))
};

# Documented in Samizdat Layer 0 spec.
fn paraGenerator(generators*) {
    # Main generator maker, which takes a list (per se) of generators
    # as a single argument.
    fn paraGenerator1(generators) {
        <> { <out> box ::
            def generatorCalls = listFilter(generators)
                { gen ::
                    def innerBox = yieldBox();
                    <> ifValue { <> gen(innerBox) }
                        { nextGenerator ::
                            <> [boxGet(innerBox), nextGenerator]
                        }
                        {
                            boxSet(box); # Void result.
                            <out>
                        }
                };
            def yields = listFilter(generatorCalls)
                { one :: <> listNth(one, 0) };
            def nexts = listFilter(generatorCalls)
                { one :: <> listNth(one, 1) };

            boxSet(box, yields);
            <> paraGenerator1(nexts);
        }
    };

    # Handle the degenerate case of no arguments.
    ifIs { <> eq(generators, []) }
        { return voidedGenerator };

    # Handle the degenerate case of a signle argument.
    ifIs { <> eq(lowSize(generators), 1) }
        { return oneItemParaGenerator(generatorFromValue(generators*)) };

    # Coerce all the original arguments to generators.
    def generatorsPerSe = listFilter(generators)
        { gen :: <> generatorFromValue(gen) };

    # Make the initial inner call (to the function defined above).
    <> paraGenerator1(generatorsPerSe)
};

# Documented in Samizdat Layer 0 spec.
fn reduceGenerator(reduceFunction, generators, baseValues) {
    # Main generator maker, which takes a generator (per se) and the
    # current reduction result (list) as its arguments.
    fn reduceGenerator1(gen, values) {
        <> { <out> box ::
            # This loop handles the fact that the reduce function is
            # allowed to yield void to indicate a generated value is to
            # be discarded.
            loopReduce(gen) { <next> gen ::
                def innerBox = yieldBox();
                ifValue { <> gen(innerBox) }
                    { nextGenerator ::
                        ifValue
                            { <> reduceFunction(boxGet(innerBox)*, values*) }
                            { nextValues ::
                                boxSet(box, nextValues);
                                <out> reduceGenerator1(
                                    nextGenerator, nextValues)
                            }
                            {
                                # The reducer discarded the values. Iterate!
                                <next> [nextGenerator]
                            }
                    };

                # The inner generator was voided.
                boxSet(box);
                <out>
            }
        }
    };

    <> reduceGenerator1(paraGenerator(generators*), baseValues)
};

# Documented in Samizdat Layer 0 spec.
fn seqGenerator(generators*) {
    # Return a voided generator if we weren't passed any arguments.
    ifIs { <> eq(generators, []) }
        { return voidedGenerator };

    # Return a plain (unwrapped) generator if we were only passed one
    # argument. (This saves some gratuitous call wrapping / double yields.)
    ifIs { <> eq(lowSize(generators), 1) }
        { return generatorFromValue(generators*) };

    fn seqGenerator1(gen) {
        <> { box ::
            def innerBox = yieldBox();
            <> ifValue { <> gen(innerBox) }
                { nextGenerator ::
                    boxSet(box, boxGet(innerBox));
                    <> seqGenerator1(nextGenerator)
                }
                {
                    def nextGenerator =
                        seqGenerator(listDelNth(generators, 0)*);
                    <> nextGenerator(box)
                }
        }
    };

    <> seqGenerator1(generatorFromValue(listNth(generators, 0)));
};

# Documented in Samizdat Layer 0 spec.
fn stringFromGenerator(generator) {
    # Avoid pointless string->generator->string conversion.
    ifIs { <> isString(generator) }
        { return generator };

    <> stringAdd(generatorFromValue(generator)*)
};

# Documented in Samizdat Layer 0 spec.
fn tokenGenerator(generator) {
    <> filterGenerator(generator)
        { elem :: <> @[(elem)] }
};

<> [
    collectFilter:       collectFilter,
    collectGenerator:    collectGenerator,
    doFilter:            doFilter,
    doGenerator:         doGenerator,
    doReduce:            doReduce,
    doReduce1:           doReduce1,
    exclusiveRange:      exclusiveRange,
    filterGenerator:     filterGenerator,
    generatorFromValue:  generatorFromValue,
    inclusiveRange:      inclusiveRange,
    mapFromGenerator:    mapFromGenerator,
    openRange:           openRange,
    optGenerator:        optGenerator,
    paraGenerator:       paraGenerator,
    reduceGenerator:     reduceGenerator,
    seqGenerator:        seqGenerator,
    stringFromGenerator: stringFromGenerator,
    tokenGenerator:      tokenGenerator
]
