# Copyright 2013 the Samizdat Authors (Dan Bornstein et alia).
# Licensed AS IS and WITHOUT WARRANTY under the Apache License,
# Version 2.0. Details: <http://www.apache.org/licenses/LICENSE-2.0>

#
# Generator functions
#


#
# Helper definitions
#

# Standard "voided" generator. This is a generator, not a generator
# constructor.
fn voidedGenerator(box) {
    boxSet(box)
};

# Empty list generator. It always yields the empty list and returns
# itself. This is a generator, not a generator constructor.
fn emptyListGenerator(box) {
    boxSet(box, []);
    <> emptyListGenerator
};

# General range generator. Takes an int starting value, an int increment,
# a "filter" function (function from int to the proper actual value, returns
# void for out-of-range int).
#
# TODO: Use this for ranges per se.
fn makeRangeGenerator(first, increment, filterFunction) {
    # Main generator maker.
    fn makeRangeGenerator1(value) {
        <> ifValue { <> filterFunction(value) }
            { result ::
                <> { box ::
                    boxSet(box, result);
                    <> makeRangeGenerator1(iadd(value, increment));
                }
            }
            { <> voidedGenerator }
    };

    <> makeRangeGenerator1(first)
};

# Makes a generator for a list.
fn makeListGenerator(list) {
    <> makeRangeGenerator(0, 1)
        { index :: <> listNth(list, index) }
};

# Makes a generator for a map.
fn makeMapGenerator(map) {
    <> makeRangeGenerator(0, 1)
        { index :: <> mapNth(map, index) }
};

# Makes a generator for a string.
fn makeStringGenerator(string) {
    <> makeRangeGenerator(0, 1)
        { index :: <> stringNth(string, index) }
};

# Degenerate generator maker for uniqlets, which are tacitly assumed to
# already be generators.
fn makeUniqletGenerator(uniqlet) {
    <> uniqlet
};

# Map from low-layer value types to corresponding appropriate generator
# maker functions.
def GENERATOR_MAKERS = [
    list:    makeListGenerator,
    map:     makeMapGenerator,
    string:  makeStringGenerator,
    uniqlet: makeUniqletGenerator
];


#
# Exported functions
#

# Documented in Samizdat Layer 0 spec.
fn filterGenerator(value, filterFunction) {
    # Main generator maker, which takes a generator (per se) as its argument.
    fn filterGenerator1(gen) {
        <> { <out> box ::
            # This loop handles the fact that the filter function is
            # allowed to yield void to indicate a generated value is to
            # be discarded.
            loopReduce(gen) { <next> gen ::
                def innerBox = yieldBox();
                ifValue { <> gen(innerBox) }
                    { nextGenerator ::
                        ifValue { <> filterFunction(boxGet(innerBox)) }
                            { value ::
                                boxSet(box, value);
                                <out> filterGenerator1(nextGenerator)
                            }
                            {
                                # The filter discarded the value. Iterate!
                                <next> nextGenerator
                            }
                    };

                # The inner generator was voided.
                boxSet(box);
                <out>
            }
        }
    };

    <> filterGenerator1(generatorFromValue(value));
};

# Documented in Samizdat Layer 0 spec.
fn generatorFromValues(values*) {
    # Main generator maker, which takes a list (per se) of generators
    # as a single argument.
    fn generatorFromValues1(generators) {
        <> { <out> box ::
            def generatorCalls = listMap(generators)
                { ., gen ::
                    def innerBox = yieldBox();
                    <> ifValue { <> gen(innerBox) }
                        { nextGenerator ::
                            <> [boxGet(innerBox), nextGenerator]
                        }
                        {
                            boxSet(box); # Void result.
                            <out>
                        }
                };
            def yields = listMap(generatorCalls)
                { ., one :: <> listNth(one, 0) };
            def nexts = listMap(generatorCalls)
                { ., one :: <> listNth(one, 1) };

            boxSet(box, yields);
            <> generatorFromValues1(nexts);
        }
    };

    # Handle the degenerate case of no arguments.
    ifValue { <> eq(values, []) }
        { . :: return voidedGenerator };

    # Coerce all the original arguments to generators.
    def generators = listMap(values)
        { ., value :: <> generatorFromValue(value) };

    # Make the initial inner call (to the function defined above).
    <> generatorFromValues1(generators)
};

# Documented in Samizdat Layer 0 spec.
fn generatorFromValue(value) {
    <> ifValue { <> mapGet(GENERATOR_MAKERS, lowType(value)) }
        { maker :: <> maker(value) }
        { io0Die("Invalid generator source.") }
};

# Documented in Samizdat Layer 0 spec.
fn listFromGenerator(value) {
    loopReduce([generatorFromValue(value), []]) { result ::
        def gen = listFirst(result);
        def list = listLast(result);
        def box = yieldBox();
        <> ifValue { <> gen(box) }
            { nextGenerator :: <> [nextGenerator, [list*, boxGet(box)]] }
            { return list }
    }
};

# Documented in Samizdat Layer 0 spec.
fn optGenerator(value) {
    # Main generator maker, which takes a generator (per se) as its argument.
    fn optGenerator1(gen) {
        <> { box ::
            def innerBox = yieldBox();
            <> ifValue { <> gen(innerBox) }
                { nextGenerator ::
                    boxSet(box, [boxGet(innerBox)]);
                    <> optGenerator1(nextGenerator)
                }
                { <> emptyListGenerator(box) }
        }
    };

    <> optGenerator1(generatorFromValue(value));
};

# Documented in Samizdat Layer 0 spec.
fn tokenGeneratorFromValue(value) {
    <> filterGenerator(value)
        { elem :: <> @[(elem)] }
};

<> [
    filterGenerator:         filterGenerator,
    generatorFromValue:      generatorFromValue,
    generatorFromValues:     generatorFromValues,
    listFromGenerator:       listFromGenerator,
    optGenerator:            optGenerator,
    tokenGeneratorFromValue: tokenGeneratorFromValue
]
