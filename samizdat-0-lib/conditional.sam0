# Copyright 2013 the Samizdat Authors (Dan Bornstein et alia).
# Licensed AS IS and WITHOUT WARRANTY under the Apache License,
# Version 2.0. Details: <http://www.apache.org/licenses/LICENSE-2.0>

#
# Conditional functions
#

# Documented in Samizdat Layer 0 spec.
fn and(predicates*) {
    <> listReduce(true, predicates)
        { result, ., predicate :: ifTrue(predicate) {} { return false } }
};

# Documented in Samizdat Layer 0 spec.
fn ifFalse(predicate, falseFunction, trueFunction?) {
    <> ifValue { <> listNth(trueFunction, 0) }
        { trueFunction :: <> ifTrue(predicate, trueFunction, falseFunction) }
        { <> ifTrue(predicate, {}, falseFunction) }
};

# Documented in Samizdat Layer 0 spec.
fn ifVoid(predicate, voidFunction, valueFunction?) {
    <> ifValue { <> listNth(valueFunction, 0) }
        { valueFunction :: <> ifValue(predicate, valueFunction, voidFunction) }
        { <> ifValue(predicate, {}, voidFunction) }
};

# Documented in Samizdat Layer 0 spec.
fn or(predicates*) {
    <> listReduce(false, predicates)
        { result, ., predicate :: ifTrue(predicate) { return true } }
};

# Documented in Samizdat Layer 0 spec.
fn \"switch"(value, cases, defaultFunction?) {
    <> ifValue { <> mapGet(cases, value) }
        { function :: <> function() }
        {
            <> ifValue { <> listNth(defaultFunction, 0) }
                { function :: <> function() }
        }
};

<> [
    and: and,
    ifFalse: ifFalse,
    ifVoid: ifVoid,
    or: or,
    switch: \"switch"
]
