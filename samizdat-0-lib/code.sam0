# Copyright 2013 the Samizdat Authors (Dan Bornstein et alia).
# Licensed AS IS and WITHOUT WARRANTY under the Apache License,
# Version 2.0. Details: <http://www.apache.org/licenses/LICENSE-2.0>

#
# Code functions
#


#
# Exported functions
#

# Documented in Samizdat Layer 0 spec.
fn forwardFunction() {
    def box = yieldBox();

    fn forwarder(args*) {
        <> ifValue { <> boxGet(box) }
            { function :: <> function(args*) }
            {
                <> ifValue { <> listFirst(args) }
                    { function ::
                        ifTrue { <> ne(lowSize(args), 1) }
                            { io0Die("Attempt to forward too many arguments.") };
                        ifFalse { <> isUniqlet(function) }
                            { io0Die("Attempt to forward to non-function.") };
                        boxSet(box, function);
                        <> function
                    }
                    { io0Die("Attempt to forward to void.") }
            }
    };

    <> forwarder
};

# Documented in Samizdat Layer 0 spec.
fn partialApply(function, args*) {
    <> { more* :: <> function(args*, more*) }
};

<> [
    forwardFunction: forwardFunction,
    partialApply: partialApply
]
