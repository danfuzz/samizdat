# Copyright 2013 the Samizdat Authors (Dan Bornstein et alia).
# Licensed AS IS and WITHOUT WARRANTY under the Apache License,
# Version 2.0. Details: <http://www.apache.org/licenses/LICENSE-2.0>

#
# Code functions
#


#
# Exported functions
#

# Documented in Samizdat Layer 0 spec.
fn forwardFunction() {
    def box = yieldBox();

    fn doForward(optFunction?, args*) {
        ifTrue { <> eq(optFunction, []) }
            { io0Die("Attempt to forward to void.") };
        ifTrue { <> ne(args, []) }
            { io0Die("Attempt to forward too many arguments.") };

        def function = optFunction*;

        ifFalse { <> isUniqlet(function) }
            { io0Die("Attempt to forward to non-function.") };

        boxSet(box, function);
        <> function
    };

    fn forwarder(args*) {
        <> ifValue { <> boxGet(box) }
            { function :: <> function(args*) }
            { <> doForward(args*) }
    };

    <> forwarder
};

# Documented in Samizdat Layer 0 spec.
fn partialApply(function, args*) {
    <> { more* :: <> function(args*, more*) }
};

<> [
    forwardFunction: forwardFunction,
    partialApply: partialApply
]
