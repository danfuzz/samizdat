# Copyright 2013 the Samizdat Authors (Dan Bornstein et alia).
# Licensed AS IS and WITHOUT WARRANTY under the Apache License,
# Version 2.0. Details: <http://www.apache.org/licenses/LICENSE-2.0>

#
# `SerialGenerator` Type
#
# Payload is `[subGens*]`, a list of sub-generators.

def Box = moduleGet({name: ["core", "Box"]});


#
# Exported Definitions
#

# Documented in spec.
fn makeSerialGenerator(generators*) {
    # Return the `nullGenerator` if we weren't passed any arguments.
    ifIs { <> eq(generators, []) }
        { return nullGenerator };

    # Return the argument directly if we were only passed one.
    # This saves some gratuitous call wrapping / double yields.
    ifIs { <> eq(sizeOf(generators), 1) }
        { return generators* };

    <> @[SerialGenerator: generators]
};

# Documented in spec.
fn SerialGenerator_nextValue(gen, box) {
    def subGens = dataOf(gen);

    <> ifValue { <> first(subGens) }
        { firstGen ->
            def moreGens = butFirst(subGens);
            def innerBox = Box::makeYieldBox();

            <> ifValue { <> nextValue(firstGen, innerBox) }
                { nextGenerator ->
                    store(box, Box::fetch(innerBox));
                    <> @[SerialGenerator: [nextGenerator, moreGens*]]
                }
                {
                    # First generator was voided.
                    <> nextValue(makeSerialGenerator(moreGens*), box)
                }
        }
        {
            # Totally voided.
            store(box)
        }
};
genericBind(nextValue, "SerialGenerator", SerialGenerator_nextValue);

<> {
    makeSerialGenerator: makeSerialGenerator
}
