# Copyright 2013 the Samizdat Authors (Dan Bornstein et alia).
# Licensed AS IS and WITHOUT WARRANTY under the Apache License,
# Version 2.0. Details: <http://www.apache.org/licenses/LICENSE-2.0>

#
# Samizdat Layer 0 tree parser
#


#
# Helper functions
#

# Returns a `call` node.
makeCall = { function actuals* ::
    <> [:@call @[@function=function @actuals=actuals]:]
};


#
# Grammar rules
#
# The following is a near-transliteration of the tree grammar in
# the *Samizdat Layer 0* specification.
#

# Parses a `highlet` node.
parseHighlet = filterRule
    (seqRule
        [:@"[":]
        [:@":":]
        @atom
        (optRule @atom)
        [:@":":]
        [:@"]":])
    { result ::
        <> apply makeCall [:@varRef @makeHighlet:]
            (listletNth result @2)
            (listletNth result @3)
    };

# Parses a `uniqlet` node.
parseUniqlet = filterRule
    [:@"@@":]
    { <> makeCall [:@varRef @makeUniqlet:] };

# Parses a `binding` node.
parseBinding = filterRule
    (seqRule @atom [:@"=":] @atom)
    { result ::
        <> @[(listletNth result @0) (listletNth result @2)]
    };

# Parses a `maplet` node.
parseMaplet = filterRule
    (seqRule [:@"@":] [:@"[":] (plusRule @binding) [:@"]":])
    { result ::
        bindings = apply listletAdd (listletNth result @2);
        <> apply makeCall [:@varRef @makeMaplet:] bindings
    };

# Parses a `emptyMaplet` node.
parseEmptyMaplet = filterRule
    (seqRule [:@"@":] [:@"[":] [:@"=":] [:@"]":])
    { <> [:@literal @[=]:] };

# Parses a `listlet` node.
parseListlet = filterRule
    (seqRule [:@"@":] [:@"[":] (plusRule @atom) [:@"]":])
    { result ::
        <> apply makeCall [:@varRef @makeListlet:] (listletNth result @2)
    };

# Parses a `emptyListlet` node.
parseEmptyListlet = filterRule
    (seqRule [:@"@":] [:@"[":] [:@"]":])
    { <> [:@literal @[]:] };

# Parses a `stringlet` node.
parseStringlet = filterRule
    (seqRule
        [:@"@":]
        (orRule [:@string:] [:@identifier:]))
    { result :: <> [:@literal (highletValue (listletNth result @1)):] };

# Parses an `intlet` node.
parseIntlet = filterRule
    (seqRule [:@"@":] (optRule [:@"-":]) [:@integer:])
    { result ::
        sign = ifTrue { <> eq (listletNth result @1) @[] }
            { <> @1 } { <> @-1 };
        value = imul sign (highletValue (listletNth result @2));
        <> [:@literal value:]
    };

# Parses a `varRef` node.
parseVarRef = filterRule
    [:@identifier:]
    { result :: <> [:@varRef (highletValue result):] };

# Parses a `varDef` node.
parseVarDef = filterRule
    (seqRule [:@identifier:] [:@"=":] @expression)
    { result ::
        name = highletValue (listletNth result @0);
        value = listletNth result @2;
        <> [:@varDef @[@name=name @value=value]:]
    };

# Parses a `parenExpression` node.
parseParenExpression = filterRule
    (seqRule [:@"(":] @expression [:@")":])
    { result :: <> listletNth result @1 };

# Parses an `atom` node.
parseAtom = orRule @varRef @intlet @stringlet
    @emptyListlet @listlet @emptyMaplet @maplet
    @uniqlet @highlet @function @parenExpression;

# Parses a `callExpression` node.
parseCallExpression = filterRule
    (seqRule @atom (plusRule @atom))
    { result ::
        <> apply makeCall (listletNth result @0) (listletNth result @1)
    };

# Parses a `unaryCallExpression` node.
parseUnaryCallExpression = filterRule
    (seqRule
        @atom
        (plusRule (seqRule [:@"(":] [:@")":])))
    { result ::
        atom = listletNth result @0;
        calls = listletNth result @1;
        <> listletReduce atom calls
            { result :: <> makeCall result }
    };

# Parses a `unaryExpression` node.
parseUnaryExpression = orRule @unaryCallExpression @atom;

# Parses an `expression` node.
parseExpression = orRule @callExpression @unaryExpression;

# Parses a `statement` node.
parseStatement = orRule @varDef @expression;

# Parses a `nonlocalExit` node.
parseNonlocalExit = filterRule
    (seqRule
        [:@"<":]
        [:@identifier:]
        [:@">":]
        (optRule @expression))
    { result ::
        varRef = [:@varRef (highletValue (listletNth result @1)):];
        actuals = listletNth result @3;
        <> apply makeCall varRef actuals
    };

# Parses a `yield` node.
parseYield = filterRule
    (seqRule [:@"<>":] @expression)
    { result :: <> listletNth result @1 };

# Parses a `yieldDef` node.
parseYieldDef = filterRule
    (seqRule [:@"<":] [:@identifier:] [:@">":])
    { result :: <> highletValue (listletNth result @1) };

# Parses a `formal` node.
parseFormal = filterRule
    (seqRule
        (filterRule
            [:@identifier:]
            { result :: <> @[@name = (highletValue result)] })
        (filterRule
            (optRule
                (orRule [:@"*":] [:@"?":]))
            { result ::
                <> ifValue { <> listletNth result @0 }
                    { repeatToken :: <> @[@repeat = repeatToken] }
                    { <> @[=] }
            }))
    { result :: <> apply mapletAdd result };

# `programBody` node helper: Parses `(statement | nonlocalExit | yield)?`,
# yielding a maplet that maps `@yield` and `@statements`. Always succeeds.
parseProgramBody1 = filterRule
    (optRule
        (orRule
            (filterRule
                (orRule @statement @nonlocalExit)
                { result :: <> @[@statements=@[result]] })
            (filterRule
                @yield
                { result :: <> @[@statements=@[] @yield=result] })))
    { result ::
        <> ifValue { <> listletNth result @0 }
            { result :: <> result }
            { <> @[@statements=@[]] }
    };

# Parses a `programBody` node.
parseProgramBody = filterRule
    (seqRule
        (starRule [:@";":])
        (starRule
            (filterRule
                (seqRule @statement (plusRule [:@";":]))
                { result :: <> listletNth result @0 }))
        parseProgramBody1
        (starRule [:@";":]))
    { result ::
        mostStatements = listletNth result @1;
        lastClause = listletNth result @2;
        statements = listletAdd mostStatements
            (mapletGet lastClause @statements);
        <> mapletPut lastClause @statements statements;
    };

# Parses a `programDeclarations` node.
parseProgramDeclarations = filterRule
    (seqRule
        (filterRule (starRule @formal)
            { result ::
                <> ifTrue { <> ne result @[] }
                    { <> @[@formals = result] }
                    { <> @[=] }
            })
        (filterRule (optRule @yieldDef)
            { result ::
                <> ifValue { <> listletNth result @0 }
                    { yieldDef :: <> @[@yieldDef = yieldDef] }
                    { <> @[=] }
            }))
    { result :: <> apply mapletAdd result };

# `program` node helper: Parses `(programDeclarations [:@"::":])?`, yielding
# a maplet (possibly empty).
parseProgram1 = filterRule
    (optRule (seqRule @programDeclarations [:@"::":]))
    { result ::
        <> ifValue { <> listletNth result @0 }
            { match :: <> listletNth match @0 }
            { <> @[=] }
    };

# Parses a `program` node.
parseProgram = filterRule
    (seqRule parseProgram1 @programBody)
    { result :: <> [:@function (apply mapletAdd result):] };

# Parses a `function` node.
parseFunction = filterRule
    (seqRule [:@"{":] @program [:@"}":])
    { result :: <> listletNth result @1 };

# Maplet of all of the parser rules. Any time a parser rule is needed,
# it's looked up through this maplet by name, which trivially allows for
# mutually-recursive rules (since the rules refer directly to names and
# not actual functions).
TREE0_RULES = @[
    @atom = parseAtom
    @binding = parseBinding
    @callExpression = parseCallExpression
    @emptyListlet = parseEmptyListlet
    @emptyMaplet = parseEmptyMaplet
    @expression = parseExpression
    @formal = parseFormal
    @function = parseFunction
    @highlet = parseHighlet
    @intlet = parseIntlet
    @listlet = parseListlet
    @maplet = parseMaplet
    @nonlocalExit = parseNonlocalExit
    @parenExpression = parseParenExpression
    @program = parseProgram
    @programBody = parseProgramBody
    @programDeclarations = parseProgramDeclarations
    @statement = parseStatement
    @stringlet = parseStringlet
    @unaryExpression = parseUnaryExpression
    @unaryCallExpression = parseUnaryCallExpression
    @uniqlet = parseUniqlet
    @varDef = parseVarDef
    @varRef = parseVarRef
    @yield = parseYield
    @yieldDef = parseYieldDef
];


#
# Main `parse` function
#

# Parses a listlet of tokens as a program, using Samizdat Layer 0 tree
# syntax. Returns a `function` node.
parse = { tokens ::
    theReader = reader tokens;
    theEngine = pegEngine theReader TREE0_RULES;
    theProgram = parseProgram theEngine;

    ifFalse { <> theEngine @eof }
        {
            io0Note @"Pending tokens:";

            { <break> ::
                loop {
                    ifValue { <> theEngine @read }
                        { token :: io0Note (format @"    %q" token) }
                        { <break> }
                }
            }();

            io0Die @"Extra tokens at end of program."
        };

    <> theProgram;
};

<> @[@parse = parse];
