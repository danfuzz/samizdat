# Copyright 2013 the Samizdat Authors (Dan Bornstein et alia).
# Licensed AS IS and WITHOUT WARRANTY under the Apache License,
# Version 2.0. Details: <http://www.apache.org/licenses/LICENSE-2.0>

#
# Structural value diff.
#

READER ::

# Forward declaration of `innerDiff`.
def innerDiff = forwardFunction();

# Reports a difference.
fn report(context, v1, v2, message) {
    io0Note(message);
    listForEach(context)
        { ., item ::
            io0Note(format("    at %Q", item));
            <> null
        };
    io0Note("");
    io0Note(format("v1 = %q", v1));
    io0Note(format("v2 = %q", v2));
};

# Diffs tokens.
fn diffToken(context, v1, v2) {
    def type1 = tokenType(v1);
    def type2 = tokenType(v2);
    def value1 = tokenValue(v1)?;
    def value2 = tokenValue(v2)?;

    <> and
        { <> innerDiff(context, "type", type1, type2) }
        { <> innerDiff(context, "value", value1, value2) }
};

# Diffs ints.
fn diffInt(context, v1, v2) {
    <> report(context, v1, v2, "Different ints.")
};

# Diffs maps.
fn diffMap(context, v1, v2) {
    def size1 = lowSize(v1);
    def size2 = lowSize(v2);

    <> and
        { <> innerDiff(context, "size", size1, size2) }
        {
            mapForEach(v1)
                { key, value ::
                    ifVoid { <> mapGet(v2, key) }
                        {
                            report([context*, key], v1, v2,
                                "Key mismatch.");
                            return
                        }
                };
            <> true
        }
        {
            mapForEach(v1)
                { key, value ::
                    def value2 = mapGet(v2, key);
                    ifNot { <> innerDiff(context, [key: key], value, value2) }
                        { return }
                };
            <> true
        }
};

# Diffs lists.
fn diffList(context, v1, v2) {
    def size1 = lowSize(v1);
    def size2 = lowSize(v2);

    <> and
        { <> innerDiff(context, "size", size1, size2) }
        {
            listForEach(v1)
                { index, elem ::
                    innerDiff(context, index, elem, listNth(v2, index))
                }
        }
};

# Diffs strings.
fn diffString(context, v1, v2) {
    <> report(context, v1, v2, "Different strings.")
};

# Diffs uniqlets.
fn diffUniqlet(context, v1, v2) {
    <> report(context, v1, v2, "Different uniqlets.")
};

# Mapping from type names to diff functions.
def DIFF_FUNCTIONS = [
    token:   diffToken,
    int:     diffInt,
    list:    diffList,
    map:     diffMap,
    string:  diffString,
    uniqlet: diffUniqlet
];

# Inner diff. Returns an `eq` result.
fn innerDiffImpl(context, newContext, v1, v2) {
    def type1 = lowType(v1);
    def type2 = lowType(v2);
    def innerContext = [context*, newContext, type1];

    <> ifIs { <> ne(v1, v2) }
        {
            def type1 = lowType(v1);
            def type2 = lowType(v2);

            <> ifIs { <> ne(type1, type2) }
                {
                    <> report(context, v1, v2,
                        format("Types differ: %q %q", type1, type2))
                }
                {
                    <> mapGet(DIFF_FUNCTIONS, type1)(innerContext, v1, v2)
                }
        }
        { <> true }
};
innerDiff(innerDiffImpl);

# Compares two values structurally, reporting on the differences.
fn diff(v1, v2) {
    <> innerDiff([], "top", v1, v2)
};

<> [diff: diff]
