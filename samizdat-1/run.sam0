# Copyright 2013 the Samizdat Authors (Dan Bornstein et alia).
# Licensed AS IS and WITHOUT WARRANTY under the Apache License,
# Version 2.0. Details: <http://www.apache.org/licenses/LICENSE-2.0>

#
# `run` function, which is the effective main entry point to the program.
#

# Parses the given program text, yielding a `program` node. This also
# compares the would-be result with the result of doing the same operation
# using the samizdat-0 facilities for same, complaining if they're not
# equal.
parseProgram = { programText ::
    # Turn the text into a parse tree using the tokenizer and tree parser
    # defined in this program.
    tokens = tokenize programText;
    programNode = parse tokens;

    # Turn the text into a parse tree using the samizdat-0 implementation
    # of parsing.
    programNode0 = sam0Tree programText;

    # Complain if the two parsed forms aren't identical.
    ifTrue { <> ne programNode0 programNode }
        {
            io0Note @"Parser disagreement.";
            diff programNode0 programNode;
            io0Note @"";
            io0Note (format @"v1\n----------\n%q\n" programNode0);
            io0Note (format @"v2\n----------\n%q\n" programNode);
            io0Die @"Ouch!";
        };

    <> programNode
};

# Compiles the file named in the first argument in a fresh context,
# and runs it, passing it an array consisting of its own file path
# (in standard listlet-of-stringlet form) followed by the rest of the
# arguments as-is.
run = { file rest* ::
    filePath = io0PathFromStringlet file;
    programText = io0ReadFileUtf8 filePath;
    programNode = parseProgram programText;

    # Evaluate the program in the context of the samizdat-0 core library.
    function = sam0Eval (makeLibrary LIBRARY) programNode;

    # Run it!
    <> apply function filePath rest
};

<> @[@run = run]
