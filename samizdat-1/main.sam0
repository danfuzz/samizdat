#!/usr/bin/env samizdat-0
#
# Copyright 2013 the Samizdat Authors (Dan Bornstein et alia).
# Licensed AS IS and WITHOUT WARRANTY under the Apache License,
# Version 2.0. Details: <http://www.apache.org/licenses/LICENSE-2.0>

#
# Samizdat Layer 1
#
# This is a Samizdat Layer 0 program which runs Samizdat Layer 0
# programs.
#


# This declares the arguments to the program itself.
SELF_PATH ARGS* ::


#
# Helper functions
#

# Reads a sibling file. Note: This is a call to a function-generating
# function.
readFile = io0SandboxedReader
    (listDelNth SELF_PATH (isub (lowSize SELF_PATH) @1));

# Loads a sibling file.
loadFile = { ctx name ::
    text = readFile [(stringAdd name @".sam0")];
    tree = sam0Tree text;
    function = sam0Eval ctx tree;

    <> function readFile
};

# Loads the named libraries, each one in the context of the previously-defined
# ones.
loadAllLibraries = { ctx rest* ::
    <> listReduce ctx rest { result elem ::
        <> mapAdd result (loadFile result elem);
    }
};

# Put together the final execution context.
CONTEXT = loadAllLibraries (makeLibrary LIBRARY)
    @diff @reader @peg @parser @parseArgs @token0 @tree0 @run;

# Call the `run` function to do the final deed.
<> apply (mapGet CONTEXT @run) ARGS
