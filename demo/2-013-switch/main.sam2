# Copyright 2013 the Samizdat Authors (Dan Bornstein et alia).
# Licensed AS IS and WITHOUT WARRANTY under the Apache License,
# Version 2.0. Details: <http://www.apache.org/licenses/LICENSE-2.0>

#
# Switch expression demo
#


# This declares the arguments to the program itself.
SELF_PATH, ARGS* ::


#
# Helper Definitions
#

fn powerName(value) {
    <> switch <out> (value) {
        1:             { <> "one" }
        2:             { <out> "two" }
        4:             { return "four" }
        8:             { <> "eight" }
        (1..4)*:       { <> "um..." }
        (5, 6, blort): { <> "wha?" }
        default:       { <> "uh..." }
    }
};

fn interpolator(value, keys) {
    <> switch (value) {
        (10, 11):  { <> "Constant override" }
        value * 0: { <> "Calculated override" }
        keys*:     { <> "Yes" }
        default:   { <> "No" }
    }
};

fn elser(item?) {
    <> switch (item*) {
        blort: { <> "See in the dark." }
        fizmo: { <> "Unclog pipes." }
        else:  { <> "Mysterious non-item." }
    }
};

fn namer(value) {
    <> switch (x := value) {
        (1, 2, 3): { <> mul(x, 10)   }
        (4, 5, 6): { <> mul(x, 100)  }
        default:   { <> mul(x, 1000) }
    }
};


#
# Main Tests
#

io0Note("Power...");
io0Note(powerName(0));
io0Note(powerName(1));
io0Note(powerName(2));
io0Note(powerName(3));
io0Note(powerName(4));
io0Note(powerName(5));
io0Note(powerName(6));
io0Note(powerName(7));
io0Note(powerName("blort"));

io0Note("\nInterpolate...");
io0Note(interpolator(1, [1, 2, 3]));
io0Note(interpolator(2, [(4..6)*]));
io0Note(interpolator(10, [(8..18)*]));
io0Note(interpolator(11, [(8..18)*]));
io0Note(interpolator(0, [0, 5]));

io0Note("\nElse...");
io0Note(elser("blort"));
io0Note(elser("fizmo"));
io0Note(elser());
io0Note(sourceString(elser("igram")?)); # `?` because we expect void.

io0Note("\nVoid yield...");
ifNot
    {
        <> switch (lt(2, 1)) {
            default: { io0Note("Huh?") }
        }
    }
    { io0Note("Yes, void.") };

io0Note("\nNames...");
io0Note(sourceString(namer(1)));
io0Note(sourceString(namer(2)));
io0Note(sourceString(namer(3)));
io0Note(sourceString(namer(4)));
io0Note(sourceString(namer(5)));
io0Note(sourceString(namer(6)));
io0Note(sourceString(namer(7)));

<> 0
