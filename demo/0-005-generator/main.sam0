# Copyright 2013 the Samizdat Authors (Dan Bornstein et alia).
# Licensed AS IS and WITHOUT WARRANTY under the Apache License,
# Version 2.0. Details: <http://www.apache.org/licenses/LICENSE-2.0>

#
# Generator function demo
#


# This declares the arguments to the program itself.
SELF_PATH, ARGS* ::


#
# Helper Definitions
#

# Prints the given value, indented slightly.
fn msg(value) {
    io0Note(cat("  ", sourceString(value)))
};

# Outputs all generated values, with an initial header, followed by
# the listification of the generator, all followed by an extra newline
# at the end.
fn msgAll(header, gen) {
    io0Note(header);

    { <done> ::
        loopReduce(gen) { gen ::
            def box = makeYieldBox();
            <> ifValue { <> nextValue(gen, box) }
                { nextGen ::
                    msg(fetch(box));
                    <> [nextGen]
                }
                { <done> }
        }
    }();

    io0Note("  [all]");
    msg(collect(gen));
    io0Note("")
};



#
# Main Tests
#

msgAll("Empty list", []);
msgAll("Empty map", [:]);
msgAll("Empty string", "");
msgAll("NullGenerator", nullGenerator);
msgAll("Empty ListWrapGenerator", makeListWrapGenerator([]));
msgAll("Empty ParaGenerator", makeParaGenerator());
msgAll("Empty SeqGenerator", makeSeqGenerator());

msgAll("List", [101, 33, 5555, "blort"]);
msgAll("Map", [one: 1, two: 2, three: 3, four: 4, five: "fizmo"]);
msgAll("String", "Happy string!");

msgAll("ListWrapGenerator", makeListWrapGenerator("frotz"));

msgAll("Single-item ParaGenerator",
    makeParaGenerator(["blort", "igram", "fizmo"]));

msgAll("All-finite ParaGenerator",
    makeParaGenerator(
        [1, 2, 3],
        "abcdefg",
        [a: "a!", b: "b!", c: "c!", d: "d!", e: "e!"]));

msgAll("ParaGenerator with opt element and ValueGenerator",
    makeParaGenerator(
        makeOptGenerator([1, 2, 3]),
        makeValueGenerator("blort"),
        "abcdefg",
        [a: "a!", b: "b!", c: "c!", d: "d!", e: "e!"]));

msgAll("FilterGenerator (square even numbers)",
    makeFilterGenerator([1, 2, 3, 4, 5, 6, 7, 8, 9])
        { value ::
            <> ifIs { <> eq(mod(value, 2), 0) }
                { <> mul(value, value) }
        });

io0Note("generatorPump on FilterGenerator");
generatorPump(
    makeFilterGenerator([1, 2, 3, 4])
        { value :: io0Note(cat("    Yay ", sourceString(value))) });
io0Note("");

msgAll("Token generator", makeTokenGenerator("Tokens!"));

msgAll("ParaGenerator with open ranges",
    makeParaGenerator(
        makeOpenRange("a", 1),
        makeOpenRange(100, 10),
        [1, 2, 3, 4, 5, 6, 7]));

msgAll("Size one SeqGenerator",
    makeSeqGenerator([a: 10, b: 20, c: 30]));

msgAll("Size two SeqGenerator",
    makeSeqGenerator(
        [a: 10, b: 20, c: 30],
        "defg"));

msgAll("Size six SeqGenerator (with a couple pre-voided generators)",
    makeSeqGenerator("abc", [], [], nullGenerator, ["def", "ghi"], []));

io0Note("collectAsMap on a FilterGenerator");
def outMap = collectAsMap(
    makeFilterGenerator(makeInclusiveRange(0, 2, 4))
        { value ::
            <> [(value): value, add(value, 1): value, blort: value]
        });
io0Note(cat("    ", sourceString(outMap)));
io0Note("");


<> 0
