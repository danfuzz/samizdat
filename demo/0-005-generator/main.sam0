# Copyright 2013 the Samizdat Authors (Dan Bornstein et alia).
# Licensed AS IS and WITHOUT WARRANTY under the Apache License,
# Version 2.0. Details: <http://www.apache.org/licenses/LICENSE-2.0>

#
# Generator function demo.
#


# This declares the arguments to the program itself.
SELF_PATH, ARGS* ::


#
# Helper function
#

# Prints the given value, indented slightly.
fn msg(value) {
    io0Note(stringAdd("  ", sourceString(value)))
};

# Outputs all generated values, with an initial header, followed by
# the listification of the generator, all followed by an extra newline
# at the end.
fn msgAll(header, gen) {
    io0Note(header);

    { <done> ::
        loopReduce(gen) { gen ::
            def box = yieldBox();
            <> ifValue { <> gen(box) }
                { nextGen ::
                    msg(boxGet(box));
                    <> [nextGen]
                }
                { <done> }
        }
    }();

    io0Note("  [all]");
    msg(listFromGenerator(gen));
    io0Note("")
};



#
# Main tests
#

msgAll("Empty list", generatorFromValue([]));
msgAll("Empty map", generatorFromValue([:]));
msgAll("Empty string", generatorFromValue(""));

msgAll("List", generatorFromValue([101, 33, 5555, "blort"]));
msgAll("Map",
    generatorFromValue([one: 1, two: 2, three: 3, four: 4, five: "fizmo"]));
msgAll("String", generatorFromValue("Happy string!"));

msgAll("All-finite paraGeneratorFromValues",
    paraGeneratorFromValues(
        [1, 2, 3],
        "abcdefg",
        [a: "a!", b: "b!", c: "c!", d: "d!", e: "e!"]));

msgAll("Paragenerator with opt element",
    paraGeneratorFromValues(
        optGenerator([1, 2, 3]),
        "abcdefg",
        [a: "a!", b: "b!", c: "c!", d: "d!", e: "e!"]));

msgAll("Filter generator (square even numbers)",
    filterGenerator([1, 2, 3, 4, 5, 6, 7, 8, 9])
        { value ::
            <> ifIs { <> eq(irem(value, 2), 0) }
                { <> imul(value, value) }
        });

msgAll("Token generator", tokenGeneratorFromValue("Tokens!"));

msgAll("Inclusive range 5..10",
    generatorForInclusiveRange(5, 1, 10));

msgAll("Inclusive range 10..-2..1",
    generatorForInclusiveRange(10, -2, 1));

msgAll("Inclusive range \"a\"..5..\"z\"",
    generatorForInclusiveRange("a", 5, "z"));

msgAll("Exclusive range 5..10",
    generatorForExclusiveRange(5, 1, 10));

msgAll("Exclusive range 10..-2..1",
    generatorForExclusiveRange(10, -2, 1));

msgAll("Exclusive range \"a\"..5..\"z\"",
    generatorForExclusiveRange("a", 5, "z"));

msgAll("ParaGenerator with open ranges",
    paraGeneratorFromValues(
        generatorForOpenRange("a", 1),
        generatorForOpenRange(100, 10),
        [1, 2, 3, 4, 5, 6, 7]));

msgAll("Zero-increment int range",
    generatorForInclusiveRange(10, 0, 20));

msgAll("Zero-increment character range",
    generatorForInclusiveRange("x", 0, "a"));

msgAll("Empty SeqGenerator",
    seqGeneratorFromValues());

msgAll("Size one SeqGenerator",
    seqGeneratorFromValues([a: 10, b: 20, c: 30]));

msgAll("Size two SeqGenerator",
    seqGeneratorFromValues(
        [a: 10, b: 20, c: 30],
        "defg"));

msgAll("Size five SeqGenerator (with a couple pre-voided generators)",
    seqGeneratorFromValues("abc", [], [], ["def", "ghi"], []));

<> 0
