# Copyright 2013 the Samizdat Authors (Dan Bornstein et alia).
# Licensed AS IS and WITHOUT WARRANTY under the Apache License,
# Version 2.0. Details: <http://www.apache.org/licenses/LICENSE-2.0>

#
# Generator function demo.
#


# This declares the arguments to the program itself.
SELF_PATH, ARGS* ::


#
# Helper function
#

# Outputs all generated values, with an initial header, followed by
# the listification of the generator, all followed by an extra newline
# at the end.
fn outAll(header, gen) {
    io0Note(header);

    { <done> ::
        loopReduce(gen) { gen ::
            def box = yieldBox();
            <> ifValue { <> gen(box) }
                { nextGen ::
                    io0Note(format("  %q", boxGet(box)));
                    <> nextGen
                }
                { <done> }
        }
    }();

    io0Note("  [fin]");
    io0Note(format("  all: %q", listFromGenerator(gen)));
    io0Note("")
};



#
# Main tests
#

outAll("Empty list", generatorFromValue([]));
outAll("Empty map", generatorFromValue([:]));
outAll("Empty string", generatorFromValue(""));

outAll("List", generatorFromValue([101, 33, 5555, "blort"]));
outAll("Map",
    generatorFromValue([one: 1, two: 2, three: 3, four: 4, five: "fizmo"]));
outAll("String", generatorFromValue("Happy string!"));

outAll("All-finite generatorFromValues",
    generatorFromValues(
        [1, 2, 3],
        "abcdefg",
        [a: "a!", b: "b!", c: "c!", d: "d!", e: "e!"]));

outAll("Cogenerator with opt element",
    generatorFromValues(
        optGenerator([1, 2, 3]),
        "abcdefg",
        [a: "a!", b: "b!", c: "c!", d: "d!", e: "e!"]));

outAll("Filter generator (square even numbers)",
    filterGenerator([1, 2, 3, 4, 5, 6, 7, 8, 9])
        { value ::
            <> ifIs { <> eq(irem(value, 2), 0) }
                { <> imul(value, value) }
        });

outAll("Token generator", tokenGeneratorFromValue("Tokens!"));

outAll("Inclusive range 5..10",
    generatorForInclusiveRange(5, 1, 10));

outAll("Inclusive range 10..-2..1",
    generatorForInclusiveRange(10, -2, 1));

outAll("Inclusive range \"a\"..5..\"z\"",
    generatorForInclusiveRange("a", 5, "z"));

outAll("Exclusive range 5..10",
    generatorForExclusiveRange(5, 1, 10));

outAll("Exclusive range 10..-2..1",
    generatorForExclusiveRange(10, -2, 1));

outAll("Exclusive range \"a\"..5..\"z\"",
    generatorForExclusiveRange("a", 5, "z"));

outAll("Cogenerator with open ranges",
    generatorFromValues(
        generatorForOpenRange("a", 1),
        generatorForOpenRange(100, 10),
        [1, 2, 3, 4, 5, 6, 7]));

outAll("Zero-increment int range",
    generatorForInclusiveRange(10, 0, 20));

outAll("Zero-increment character range",
    generatorForInclusiveRange("x", 0, "a"));


<> 0
