# Copyright 2013 the Samizdat Authors (Dan Bornstein et alia).
# Licensed AS IS and WITHOUT WARRANTY under the Apache License,
# Version 2.0. Details: <http://www.apache.org/licenses/LICENSE-2.0>

#
# Generator function demo
#


# This declares the arguments to the program itself.
SELF_PATH, ARGS* ->

def Box = moduleGet({name: ["core", "Box"]});
def Generator = moduleGet({name: ["core", "Generator"]});
def Io0 = moduleGet({name: ["core", "Io0"]});


#
# Helper Definitions
#

# Prints the given value, indented slightly.
fn msg(value) {
    Io0::note(cat("  ", Format::source(value)))
};

# Outputs all generated values, with an initial header, followed by
# the listification of the generator, all followed by an extra newline
# at the end.
fn msgAll(header, gen) {
    Io0::note(header);

    { <done> ->
        loopReduce(gen) { gen ->
            def box = Box::makeYieldBox();
            <> ifValue { <> nextValue(gen, box) }
                { nextGen ->
                    msg(Box::fetch(box));
                    <> [nextGen]
                }
                { <done> }
        }
    }();

    Io0::note("  [all]");
    msg(collect(gen));
    Io0::note("")
};



#
# Main Tests
#

msgAll("Empty list", []);
msgAll("Empty map", {});
msgAll("Empty string", "");
msgAll("NullGenerator", Generator::nullGenerator);
msgAll("Empty ListWrapGenerator", makeListWrapGenerator([]));
msgAll("Empty ParaGenerator", makeParaGenerator());
msgAll("Empty SerialGenerator", makeSerialGenerator());

msgAll("List", [101, 33, 5555, "blort"]);
msgAll("Map", {one: 1, two: 2, three: 3, four: 4, five: "fizmo"});
msgAll("String", "Happy string!");

msgAll("ListWrapGenerator", makeListWrapGenerator("frotz"));

msgAll("Single-item ParaGenerator",
    makeParaGenerator(["blort", "igram", "fizmo"]));

msgAll("All-finite ParaGenerator",
    makeParaGenerator(
        [1, 2, 3],
        "abcdefg",
        {a: "a!", b: "b!", c: "c!", d: "d!", e: "e!"}));

msgAll("ParaGenerator with opt element and ValueGenerator",
    makeParaGenerator(
        makeOptGenerator([1, 2, 3]),
        makeValueGenerator("blort"),
        "abcdefg",
        {a: "a!", b: "b!", c: "c!", d: "d!", e: "e!"}));

msgAll("FilterGenerator (square even numbers)",
    Generator::makeFilterGenerator([1, 2, 3, 4, 5, 6, 7, 8, 9])
        { value ->
            <> ifIs { <> eq(mod(value, 2), 0) }
                { <> mul(value, value) }
        });

Io0::note("generatorPump on FilterGenerator");
Generator::generatorPump(
    Generator::makeFilterGenerator([1, 2, 3, 4])
        { value -> Io0::note(cat("    Yay ", Format::source(value))) });
Io0::note("");

msgAll("ParaGenerator with open ranges",
    makeParaGenerator(
        makeOpenRange("a", 1),
        makeOpenRange(100, 10),
        [1, 2, 3, 4, 5, 6, 7]));

msgAll("Size one SerialGenerator",
    makeSerialGenerator({a: 10, b: 20, c: 30}));

msgAll("Size two SerialGenerator",
    makeSerialGenerator(
        {a: 10, b: 20, c: 30},
        "defg"));

msgAll("Size six SerialGenerator (with a couple pre-voided generators)",
    makeSerialGenerator(
        "abc",
        [],
        [],
        Generator::nullGenerator,
        ["def", "ghi"],
        []));

Io0::note("collectAsMap on a FilterGenerator");
def outMap = Generator::collectAsMap(
    Generator::makeFilterGenerator(makeInclusiveRange(0, 2, 4))
        { value ->
            <> {(value): value, (add(value, 1)): value, blort: value}
        });
Io0::note(cat("    ", Format::source(outMap)));
Io0::note("");


<> 0
