# Copyright 2013 the Samizdat Authors (Dan Bornstein et alia).
# Licensed AS IS and WITHOUT WARRANTY under the Apache License,
# Version 2.0. Details: <http://www.apache.org/licenses/LICENSE-2.0>

#
# For loop expression demo
#


# This declares the arguments to the program itself.
SELF_PATH, ARGS* ->


#
# Helper Definitions
#

# Output the given values.
fn print(values*) {
    io0Note("    \%Q(values)")
};


#
# Main Tests
#

io0Note("Empty content.");
for (x in []) {
    io0Die("Shouldn't happen.");
};

io0Note("\nSingle element list.");
for (x in [1]) {
    print(x)
};

io0Note("\nTwo element list.");
for (x in [@blort, @fizmo]) {
    print(x)
};

io0Note("\nThree element list.");
for (x in ["muffin", "biscuit", "scone"]) {
    print(x)
};

io0Note("\nString.");
for (x in "Blort!") {
    print(x)
};

io0Note("\nFive-element map, no name binding.");
for (. in {a:1, b:2, c:3, d:4, e:5}) {
    print("(item)")
};

io0Note("\nList and string in parallel.");
for (x in [0, 1, 1, 2, 3, 5, 8, 13, 21, 34, 55], y in "fibonacci") {
    print(x, y)
};

io0Note("\nExplicit generator.");
for (value in makeFilterGenerator({ x <> x * x }, [1, 2, 3, 4])) {
    print(value)
};

io0Note("\nUnnamed break.");
def result1 = for (x in [1, 2, 3, 4, 5]) {
    if (x \== 3) { break 23 };
    print(x)
};
io0Note("Result: \%q(result1)");

io0Note("\nNamed break.");
def result2 = for <out> (x in [1, 2, 3, 4, 5]) {
    if (x \== 3) { <out> 187 };
    print(x)
};
io0Note("Result: \%q(result2)");

io0Note("\nUnnamed continue.");
for (x in [1, 2, 3, 4, 5]) {
    if (x \== 3) { continue };
    print(x)
};

io0Note("\nNamed continue.");
for (x in [1, 2, 3, 4, 5]) { <next> ->
    if (x \== 3) { <next> };
    print(x)
};

<> 0
