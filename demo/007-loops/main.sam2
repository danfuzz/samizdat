# Copyright 2013 the Samizdat Authors (Dan Bornstein et alia).
# Licensed AS IS and WITHOUT WARRANTY under the Apache License,
# Version 2.0. Details: <http://www.apache.org/licenses/LICENSE-2.0>

#
# Loop expression demo.
#


# This declares the arguments to the program itself.
SELF_PATH, ARGS* ::


#
# Main tests
#

def box = mutableBox(0);

while (boxGet(box) < 10) {
    boxSet(box, boxGet(box) + 1);
    io0Note(format("Value is %q.", boxGet(box)))
};

while (boxGet(box) < 20) {
    boxSet(box, boxGet(box) + 1);
    if (boxGet(box) < 15) {
        continue
    };
    io0Note(format("Value is %q.", boxGet(box)))
};

while (boxGet(box) < 30) { <next> ::
    boxSet(box, boxGet(box) + 1);
    if (boxGet(box) < 25) {
        <next>
    };
    io0Note(format("Value is %q.", boxGet(box)))
};

def result1 = while (boxGet(box) < 40) {
    boxSet(box, boxGet(box) + 1);
    if (boxGet(box) == 35) {
        break "blort"
    };
    io0Note(format("Value is %q.", boxGet(box)))
};

io0Note(format("Result 1 is %q.", result1));

def result2 = while <out> (boxGet(box) < 50) {
    boxSet(box, boxGet(box) + 1);
    if (boxGet(box) == 45) {
        <out> "fizmo"
    };
    io0Note(format("Value is %q.", boxGet(box)))
};

io0Note(format("Result 2 is %q.", result2));

do {
    boxSet(box, boxGet(box) + 1);
    if (boxGet(box) == 55) {
        break
    };
    io0Note(format("Value is %q.", boxGet(box)))
};

def result3 = do <out> {
    boxSet(box, boxGet(box) + 1);
    if (boxGet(box) == 60) {
        <out> "igram"
    };
    io0Note(format("Value is %q.", boxGet(box)))
};

io0Note(format("Result 3 is %q.", result3));

do {
    boxSet(box, boxGet(box) + 1);
    if (boxGet(box) < 65) {
        continue
    };
    io0Note(format("Value is %q.", boxGet(box)))
} while(boxGet(box) < 70);

<> 0
